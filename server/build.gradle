plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

group = 'su.nepom'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //-- Spring
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'

    //-- swagger
    implementation 'org.springdoc:springdoc-openapi-ui:1.2.32'

    //-- mapstruct
    def mapstruct = '1.3.1.Final'
    implementation "org.mapstruct:mapstruct:${mapstruct}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct}"

    //-- БД
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'com.opentable.components:otj-pg-embedded:0.13.3'

    //-- Hibernate metamodel
    annotationProcessor('org.hibernate:hibernate-jpamodelgen')

    // для logback.xml (только для тестов)
    testImplementation(group: 'org.codehaus.janino', name: 'janino', version: '3.1.2')

    // logbook - логирование http запросов и ответов
    implementation 'org.zalando:logbook-spring-boot-starter:2.4.1'


    //-- модули
    implementation project(":util")
    implementation project(":dto")

    //-- Lombok
    def lombok = 'org.projectlombok:lombok'
    compileOnly lombok
    annotationProcessor lombok
    testCompileOnly lombok
    testAnnotationProcessor lombok
}

test {
    useJUnitPlatform()
}
